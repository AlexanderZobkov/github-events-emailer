plugins {
    id 'groovy'
    id 'org.springframework.boot' version '2.3.3.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id "ru.vyarus.quality" version "4.3.0"
    id 'com.google.cloud.tools.jib' version '2.5.0'
    id "maven-publish"
    id "com.jfrog.artifactory" version '4.17.2'
}

group 'com.github.alexander-zobkov'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

jib {
    from {
        image = 'gcr.io/distroless/java:debug'
    }
    to {
        // Sometimes you need docker.io explicitly specified however the jib gradle plugin just chomp it.
        image = "docker.io/docker.io/zobkov/${rootProject.name}"
    }
    container {
        mainClass = 'com.github.zobkov.webhook.App'
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.apache.camel.springboot:camel-spring-boot-dependencies:3.5.0"
    }
}

dependencies {
    implementation 'org.kohsuke:github-api:1.116'
    implementation 'org.codehaus.groovy:groovy-all:3.0.5'
    implementation 'org.apache.camel.springboot:camel-spring-boot-starter'
    implementation 'org.apache.camel:camel-jetty'
    implementation 'org.apache.camel:camel-mail'
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'

    testImplementation group: 'junit', name: 'junit', version: '4.13'
    testImplementation 'org.apache.camel:camel-test-spring'
    testImplementation 'org.apache.camel:camel-spring-javaconfig'
}

configurations {
    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
}

tasks.build.dependsOn tasks.jibDockerBuild

quality {
    // To workaround: java.lang.NoClassDefFoundError: Lgroovy/text/TemplateEngine;
    codenarcVersion = '1.6'
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
    tasks.named("build").configure { it.dependsOn sourceJar }
}

bootJar {
    archiveClassifier = 'boot'
    manifest {
        attributes 'Main-Class': 'org.springframework.boot.loader.PropertiesLauncher'
    }
}

bootRun {
    doFirst {
        def props = new Properties()
        file("application-tests.properties").withInputStream { props.load(it) }
        systemProperties = props.collectEntries { it }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact bootJar
            groupId rootProject.group
            artifactId rootProject.name
            version rootProject.version
            pom.packaging = 'jar'
        }
    }
}

artifactory {
    contextUrl = 'https://oss.jfrog.org'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = System.getProperty('bintray.user')
            password = System.getProperty('bintray.key')
        }
        defaults {
            publications('maven')
            publishArtifacts = true
            publishPom = true
        }
    }
    clientConfig.info.setBuildNumber(System.getProperty('build.number'))
}