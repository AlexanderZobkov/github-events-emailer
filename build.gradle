plugins {
    id 'groovy'
    id 'org.springframework.boot' version '2.7.5'
    id 'io.spring.dependency-management' version '1.1.0'
    id "ru.vyarus.quality" version "4.8.0"
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
    id "maven-publish"
}

group 'com.github.alexander-zobkov'
version '1.0-SNAPSHOT'

sourceSets {
    integrationTest {
        groovy.srcDir "$projectDir/src/integrationTest/groovy"
        resources.srcDir "$projectDir/src/integrationTest/resources"
    }
}

// https://github.com/gradle/gradle/issues/17236
tasks.withType(Copy).all {
    duplicatesStrategy 'exclude'
}

bootBuildImage {
    imageName = "docker.io/zobkov/${project.name}"
    docker {
        publishRegistry {
            username = System.getenv('DOCKER_USER')?:'DOCKER_USER is not set'
            password = System.getenv('DOCKER_PASS')?:'DOCKER_PASS is not set'
            url = "https://index.docker.io/v2/"
        }
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.apache.camel.springboot:camel-spring-boot-dependencies:3.19.0"
        mavenBom "org.springframework:spring-framework-bom:5.3.23"
        mavenBom 'org.codehaus.groovy:groovy-bom:3.0.10'
    }
}

dependencies {
    implementation 'org.kohsuke:github-api:1.313'
    implementation 'org.codehaus.groovy:groovy-xml'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.slf4j:jul-to-slf4j'
    implementation 'org.apache.camel.springboot:camel-spring-boot-starter'
    implementation 'org.apache.camel:camel-jetty'
    implementation 'org.apache.camel:camel-mail'
    implementation 'org.apache.camel:camel-micrometer'
    implementation 'org.apache.camel:camel-management'
    implementation 'org.apache.camel:camel-endpointdsl'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation 'org.apache.camel:camel-test-spring-junit5'
    testImplementation platform('org.spockframework:spock-bom:2.1-groovy-3.0')
    testImplementation 'org.spockframework:spock-core'
    testImplementation 'org.spockframework:spock-junit4'

    integrationTestImplementation 'org.testcontainers:spock:1.17.5'
    integrationTestImplementation 'org.codehaus.groovy:groovy-json'
    integrationTestImplementation 'com.github.alexdlaird:java-ngrok:1.5.6'
    integrationTestImplementation 'org.eclipse.jgit:org.eclipse.jgit:6.3.0.202209071007-r'
    integrationTestImplementation 'org.awaitility:awaitility:4.2.0'
}

configurations {
    runtimeClasspath.shouldResolveConsistentlyWith(compileClasspath)
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
    tasks.named("build").configure { it.dependsOn sourceJar }
}

bootJar {
    archiveClassifier = 'boot'
    manifest {
        attributes 'Main-Class': 'org.springframework.boot.loader.PropertiesLauncher'
    }
}

springBoot {
    buildInfo {
        properties {
            time = null
        }
    }
}

bootRun {
    doFirst { JavaExec task ->
        Properties props = new Properties()
        file("application-dev.properties").withInputStream { props.load(it) }
        props.each {
            task.environment(it.key, it.value)
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact bootJar
            groupId rootProject.group
            artifactId rootProject.name
            version rootProject.version
            pom.packaging = 'jar'
        }
    }
}

test {
    useJUnitPlatform()
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    group 'verification'
    description "Runs integration tests"
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    useJUnitPlatform()
    doFirst { Test task ->
        if (!Boolean.parseBoolean(System.getenv('CI'))) {
            Properties props = new Properties()
            file("integrationTests-dev.properties").withInputStream { props.load(it) }
            props.each {
                task.environment(it.key, it.value)
            }
        }
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
