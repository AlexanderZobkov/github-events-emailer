plugins {
    id 'groovy'
    id 'org.springframework.boot' version '2.3.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id "ru.vyarus.quality" version "4.3.0"
    id 'com.google.cloud.tools.jib' version '2.4.0'
}

group 'com.github.zobkov'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

jib {
    from {
        image = 'gcr.io/distroless/java:debug'
    }
    to {
        // Sometimes you need docker.io explicitly specified however the jib gradle plugin just chomp it.
        image = "docker.io/docker.io/zobkov/${rootProject.name}"
    }
    container {
        mainClass = 'com.github.zobkov.webhook.App'
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.apache.camel.springboot:camel-spring-boot-dependencies:3.4.2"
    }
}

dependencies {
    implementation 'org.kohsuke:github-api:1.115'
    implementation 'org.codehaus.groovy:groovy-all:3.0.5'
    implementation 'org.apache.camel.springboot:camel-spring-boot-starter'
    implementation 'org.apache.camel:camel-jetty'
    implementation 'org.apache.camel:camel-mail'

    testImplementation group: 'junit', name: 'junit', version: '4.13'
    testImplementation 'org.apache.camel:camel-test-spring'
    testImplementation 'org.apache.camel:camel-spring-javaconfig'
}

tasks.build.dependsOn tasks.jibDockerBuild

quality {
    // To workaround: java.lang.NoClassDefFoundError: Lgroovy/text/TemplateEngine;
    codenarcVersion = '1.6'
}

bootJar {
    manifest {
        attributes 'Main-Class': 'org.springframework.boot.loader.PropertiesLauncher'
    }
}

bootRun {
    doFirst {
        def props = new Properties()
        file("application-tests.properties").withInputStream { props.load(it) }
        systemProperties = props.collectEntries { it }
    }
}